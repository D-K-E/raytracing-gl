#version 430
layout(local_size_x = 1, local_size_y = 1) in; // local work_group_size
#define SCENE_OBJECT_NB 2
layout(rgba32f, location = 0, binding = 0) writeonly uniform image2D img_output;
layout(rgba32f, location = 1, binding = 1) readonly uniform image2D in_image;

// start constants
// constants.hpp
float PI = 3.1415926535;
float INFINITY = 1.0 / 0.0;
// end constants
//
// --------------------- utility functions ------------------------------
float degree_to_radian(float degree) {
  //
  return degree * PI / 180.0;
}
float rand(vec2 co) {
  // random gen
  float a = 12.9898;
  float b = 78.233;
  float c = 43758.5453;
  float dt = dot(co.xy, vec2(a, b));
  float sn = mod(dt, PI);
  return fract(sin(sn) * c);
}
float random_double() {
  // random double
  return rand(vec2(0.0, 1.0));
}
float random_double(float mi, float mx) {
  // random double
  return rand(vec2(mi, mx));
}
int random_int(int mi, int mx) { return int(random_double(mi, mx)); }
vec3 random_vec() {
  // random vector
  return vec3(random_double(), random_double(), random_double());
}
vec3 random_vec(float mi, float ma) {
  // random vector in given seed
  return vec3(random_double(mi, ma), random_double(mi, ma),
              random_double(mi, ma));
}
vec3 random_in_unit_sphere() {
  // random in unit sphere
  while (true) {
    //
    vec3 v = random_vec(-1.0, 1.0);
    if (dot(v, v) >= 1.0) {
      continue;
    }
    return v;
  }
}
vec3 random_unit_vector() {
  // unit vector
  float a = random_double(0, 2 * PI);
  float z = random_double(-1, 1);
  float r = sqrt(1 - z * z);
  return vec3(r * cos(a), r * sin(a), z);
}
vec3 random_in_hemisphere(vec3 normal) {
  // normal ekseninde dagilan yon
  vec3 unit_sphere_dir = random_in_unit_sphere();
  if (dot(unit_sphere_dir, normal) > 0.0) {
    return unit_sphere_dir;
  } else {
    return -1 * unit_sphere_dir;
  }
}
vec3 random_in_unit_disk() {
  // lens yakinsamasi iÃ§in gerekli
  while (true) {
    vec3 point = vec3(random_double(-1, 1), random_double(-1, 1), 0);
    if (dot(point, point) >= 1) {
      continue;
    }
    return point;
  }
}
vec3 refract_vec(vec3 uv, vec3 normal, float eta_over) {
  //
  float cos_theta = dot(-uv, normal);
  vec3 outpar = eta_over * (uv + (cos_theta * normal));
  vec3 out_prep = -sqrt(1.0 - dot(outpar, outpar)) * normal;
  return outpar + out_prep;
}

vec3 mixv(float mi, float ma, vec3 val) {
  //
  return vec3(mix(mi, ma, val.x), mix(mi, ma, val.y), mix(mi, ma, val.z));
}

vec3 fix_color(vec3 pcolor, int samples_per_pixel) {
  // scale sample
  pcolor /= samples_per_pixel;
  return clamp(sqrt(pcolor), 0.0, 0.999);
  // return mixv(0.0, 0.999, sqrt(pcolor));
}

void get_sphere_uv(in vec3 p, out float u, out float v) {
  //
  float phi = atan(p.z, p.x);
  float theta = asin(p.y);
  u = 1 - (phi + PI) / (2 * PI);
  v = (theta + PI / 2) / PI;
}

// --------------------- utility functions end --------------------------

//
struct Ray {
  vec3 origin;
  vec3 direction;
};
Ray makeRay(vec3 orig, vec3 dir) {
  Ray r;
  r.origin = orig;
  r.direction = dir;
  return r;
}

vec3 at(Ray r, float dist) { return r.direction * dist + r.origin; }

struct Material;
struct HitRecord {
  vec3 point;
  vec3 normal;
  float dist;
  bool front_face;
  float u, v;
  Material mat_ptr;
};
HitRecord makeRecord(vec3 p, vec3 n, float d, bool ff, Material mat) {
  HitRecord rec;
  rec.point = p;
  rec.normal = n;
  rec.dist = d;
  rec.front_face = ff;
  rec.mat_ptr = mat;
  return rec;
}
void set_face_normal(inout HitRecord rec, in Ray r, in vec3 out_normal) {
  // set face normal to hit record did we hit front or back
  rec.front_face = dot(r.direction, out_normal) < 0;
  rec.normal = (rec.front_face) ? out_normal : -1 * out_normal;
}

HitRecord makeRecord(vec3 p, vec3 n, float d, bool ff) {
  HitRecord rec;
  rec.point = p;
  rec.normal = n;
  rec.dist = d;
  rec.front_face = ff;
  return rec;
}
// ----------------- texture.hpp --------------------------
struct SolidTexture {
  vec3 color;
};
SolidTexture makeSolidTexture(vec3 c) {
  SolidTexture s;
  s.color = c;
  return s;
}
vec3 solidValue(SolidTexture s, float u, float v, in vec3 p) { return s.color; }

struct CheckeredTexture {
  SolidTexture odd;
  SolidTexture even;
};
CheckeredTexture makeCheckered(SolidTexture s1, SolidTexture s2) {
  CheckeredTexture c;
  c.odd = s1;
  c.even = s2;
  return c;
}
vec3 checkeredValue(CheckeredTexture c, float u, float v, in vec3 p) {
  //
  float sines = sin(10 * p.x) * sin(10 * p.y) * sin(10 * p.z);
  if (sines < 0)
    return solidValue(c.odd, u, v, p);
  else
    return solidValue(c.even, u, v, p);
}

struct ImageTexture {
  int width;
  int height;
};

ImageTexture makeImageTexture() {
  ImageTexture im;
  ivec2 img_dims = imageSize(in_image); // image dimensions
  im.width = img_dims.x;
  im.height = img_dims.y;
  return im;
}

vec3 imageValue(ImageTexture im, float u, float v, in vec3 p) {
  u = clamp(u, 0.0, 1.0);
  v = 1 - clamp(v, 0.0, 1.0);
  int i = int(u * im.width);
  int j = int(v * im.height);
  if (i >= im.width) {
    i = im.width - 1;
  }
  if (j >= im.height) {
    j = im.height - 1;
  }
  vec3 color = imageLoad(in_image, ivec2(i, j)).xyz;
  if (color == vec3(0)) {
    color = vec3(0, u, v);
  }
  return color;
}
struct Texture {
  int type; // 0 solid, 1 checkered, 2 image
  SolidTexture solid;
  CheckeredTexture checkered;
  ImageTexture img;
};
vec3 textureValue(Texture t, float u, float v, in vec3 p) {
  //
  if (t.type == 0) {
    return solidValue(t.solid, u, v, p);
  } else if (t.type == 1) {
    return checkeredValue(t.checkered, u, v, p);
  } else if (t.type == 2) {
    return imageValue(t.img, u, v, p);
  }
}
//----------------- end texture.hpp -----------------------

// ---------------------- start material.hpp ---------------------------
struct Lambert {
  Texture albedo;
};

Lambert makeLambert(Texture t) {
  Lambert lam;
  lam.albedo = t;
  return lam;
}

bool scatterLambert(Lambert lam, in Ray ray_in, in HitRecord record,
                    out vec3 attenuation, out Ray ray_out) {

  // isik kirilsin mi kirilmasin mi
  vec3 out_dir = record.normal + random_unit_vector();
  ray_out = makeRay(record.point, out_dir);
  attenuation = textureValue(lam.albedo, record.u, record.v, record.point);
  return true;
}

struct Metal {
  vec3 albedo;
  float roughness;
};
Metal makeMetal(vec3 alb, float fuzz) {
  Metal m;
  m.albedo = alb;
  m.roughness = fuzz;
  return m;
}
bool scatterMetal(Metal met, in Ray ray_in, in HitRecord record,
                  inout vec3 attenuation, inout Ray ray_out) {

  vec3 unit_in_dir = normalize(ray_in.direction);
  vec3 out_dir = reflect(unit_in_dir, record.normal);
  ray_out =
      makeRay(record.point, out_dir + met.roughness * random_in_unit_sphere());
  attenuation = met.albedo;
  return dot(ray_out.direction, record.normal) > 0.0;
}

struct Dielectric {
  float ref_idx;
};

Dielectric makeDielectric(float rfidx) {
  Dielectric die;
  die.ref_idx = rfidx;
  return die;
}

float fresnelCT(float costheta, float ridx) {
  // cook torrence fresnel equation
  float etao = 1 + sqrt(ridx);
  float etau = 1 - sqrt(ridx);
  float eta = etao / etau;
  float g = sqrt(pow(eta, 2) + pow(costheta, 2) - 1);
  float g_c = g - costheta;
  float gplusc = g + costheta;
  float gplus_cc = (gplusc * costheta) - 1;
  float g_cc = (g_c * costheta) + 1;
  float oneplus_gcc = 1 + pow(gplus_cc / g_cc, 2);
  float half_plus_minus = 0.5 * pow(g_c / gplusc, 2);
  return half_plus_minus * oneplus_gcc;
}
float fresnelSchlick(float costheta, float ridx) {
  //
  float r0 = (1 - ridx) / (1 + ridx);
  r0 = r0 * r0;
  return r0 + (1 - r0) * pow((1 - costheta), 5);
}
float get_fresnel(float costheta, float ridx, int choice = 0) {
  // compute freshnel
  float fresnel;
  switch (choice) {
  case 0:
    fresnel = fresnelSchlick(costheta, ridx);
    break;
  case 1:
    fresnel = fresnelCT(costheta, ridx);
    break;
  default:
    fresnel = fresnelSchlick(costheta, ridx);
    break;
  }
  return fresnel;
}

bool scatterDielectric(Dielectric diel, in Ray r_in, in HitRecord record,
                       inout vec3 attenuation, inout Ray r_out) {
  // ray out
  attenuation = vec3(1.0);
  vec3 unit_in_dir = normalize(r_in.direction);
  float eta_over = record.front_face ? 1.0 / diel.ref_idx : diel.ref_idx;
  float costheta = min(dot(-1 * unit_in_dir, record.normal), 1.0);
  float sintheta = sqrt(1.0 - costheta * costheta);
  vec3 ref;
  if (eta_over * sintheta > 1.0) {
    //
    ref = reflect(unit_in_dir, record.normal);
    r_out = makeRay(record.point, ref);
    return true;
  }
  //
  double fresnel_term = get_fresnel(costheta, eta_over);
  if (random_double() < fresnel_term) {
    ref = reflect(unit_in_dir, record.normal);
    r_out = makeRay(record.point, ref);
    return true;
  }
  ref = refract_vec(unit_in_dir, record.normal, eta_over);
  r_out = makeRay(record.point, ref);
  return true;
}

struct Material {
  int type; // 0: lambert, 1: metal, 2: dielectric;
  Lambert lam;
  Metal met;
  Dielectric die;
};

bool scatter(in Material mat_ptr, in Ray ray_in, in HitRecord record,
             out vec3 attenuation, out Ray ray_out) {
  // scatter material
  if (mat_ptr.type == 0) {
    return scatterLambert(mat_ptr.lam, ray_in, record, attenuation, ray_out);
  } else if (mat_ptr.type == 1) {
    return scatterMetal(mat_ptr.met, ray_in, record, attenuation, ray_out);
  } else if (mat_ptr.type == 2) {
    return scatterDielectric(mat_ptr.die, ray_in, record, attenuation, ray_out);
  } else {
    return false;
  }
}
// --------------------------- end material ------------------------------
// --------------------------- start camera.hpp --------------------------
struct Camera {
  vec3 lower_left_corner;
  vec3 origin;
  vec3 vertical;
  vec3 horizontal;
  vec3 u;
  vec3 v;
  vec3 w;
  float lens_radius;
  float time0; // shutter open time
  float time1; // shutter close time
};

Camera makeCamera(vec3 pos, vec3 target, vec3 up, float vfov,
                  float aspect_ratio, float aperture, float focus_dist,
                  float t0, float t1) {
  // make camera struct
  Camera cam;
  cam.origin = pos;
  cam.time0 = t0;
  cam.time1 = t1;
  cam.lens_radius = aperture / 2;

  float theta = degree_to_radian(vfov);
  float half_height = tan(theta / 2);
  half_height *= 2.0;
  float half_width = aspect_ratio * half_height;

  // w, v, u eksenleri
  cam.w = normalize(pos - target);
  cam.u = normalize(cross(up, cam.w));
  cam.v = cross(cam.w, cam.u);

  cam.horizontal = half_width * focus_dist * cam.u;
  cam.vertical = half_height * focus_dist * cam.v;

  cam.lower_left_corner =
      cam.origin - cam.horizontal / 2 - cam.vertical / 2 - cam.w * focus_dist;
  return cam;
}
Camera makeCamera() {
  return makeCamera(vec3(0, 0, -1), // pos
                    vec3(0, 0, 0),  // target
                    vec3(0, 1, 0),  // vup
                    40,             // vfov
                    1,              // aspect_ratio
                    0,              // aperature
                    10,             // focus_dist
                    0,              // shutter open time
                    1               // shutter close time
                    );
}

Ray get_ray(Camera ca, float u, float v) {
  // get camera ray
  vec3 rd = ca.lens_radius * random_in_unit_disk();
  vec3 offst = ca.u * rd.x + ca.v * rd.y;
  vec3 r_origin = ca.origin + offst;
  vec3 r_dir =
      ca.lower_left_corner + (u * ca.horizontal) + (v * ca.vertical) - r_origin;
  return makeRay(r_origin, r_dir);
}

//----------------- end camera.hpp -----------------------
struct Sphere {
  vec3 center;
  float radius;
  Material mat_ptr;
};

Sphere makeSphere(vec3 cent, float r, Material mat_ptr) {
  Sphere sp;
  sp.center = cent;
  sp.radius = r;
  sp.mat_ptr = mat_ptr;
  return sp;
}

bool hitSphere(in Sphere s, in Ray r, float dist_min, float dist_max,
               inout HitRecord record) {
  // kureye isin vurdu mu onu test eden fonksiyon
  vec3 origin_to_center = r.origin - s.center;
  float a = dot(r.direction, r.direction);
  float half_b = dot(origin_to_center, r.direction);
  float c = dot(origin_to_center, origin_to_center) - s.radius * s.radius;
  float isHit = half_b * half_b - a * c;
  float margin;
  if (isHit > 0) {
    float root = sqrt(isHit);
    margin = (-1 * half_b - root) / a;
    if (margin < dist_max && margin > dist_min) {
      record.dist = margin;
      record.point = at(r, record.dist);
      vec3 out_normal = (record.point - s.center) / s.radius;
      set_face_normal(record, r, out_normal);
      get_sphere_uv(out_normal, record.u, record.v);
      record.mat_ptr = s.mat_ptr;
      return true;
    }
    margin = (-1 * half_b + root) / a;
    if (margin < dist_max && margin > dist_min) {
      record.dist = margin;
      record.point = at(r, record.dist);
      vec3 out_normal = (record.point - s.center) / s.radius;
      set_face_normal(record, r, out_normal);
      get_sphere_uv(out_normal, record.u, record.v);
      record.mat_ptr = s.mat_ptr;
      return true;
    }
  }
  return false;
}

struct SceneObj {
  int type; // 0 sphere, 1, other
  Sphere sp;
};

struct Scene {
  SceneObj ss[SCENE_OBJECT_NB];
  bool isEmpty;
};

Scene sliceScene(in Scene scn, int s, int e) {
  // slice scene
  Scene sout;
  if ((s < 0) || (e > SCENE_OBJECT_NB)) {
    sout.isEmpty = true;
    return sout;
  }
  int k = 0;
  for (int i = s; i < e; i++) {
    sout.ss[k] = scn.ss[i];
    k += 1;
  }
  sout.isEmpty = false;
  return sout;
}

bool hit_scene(in Scene scene, in Ray r, float dmin, float dmax,
               inout HitRecord record) {
  // check if sphere is hit
  HitRecord temp;
  bool hit_ = false;
  float current_closest = dmax;
  for (int i = 0; i < SCENE_OBJECT_NB; i++) {
    SceneObj sobj = scene.ss[i];
    if (sobj.type == 0) {
      if (hitSphere(sobj.sp, r, dmin, current_closest, temp)) {
        hit_ = true;
        current_closest = temp.dist;
        record = temp;
      }
    }
  }
  return hit_;
}

vec3 ray_color(in Ray r, in Scene scene, int depth) {
  //
  Ray r_in;
  r_in.origin = r.origin;
  r_in.direction = r.direction;
  vec3 bcolor = vec3(1);

  while (true) {
    if (depth <= 0) {
      //
      return vec3(0);
      // return bcolor;
    }
    HitRecord rec;
    if (hit_scene(scene, r_in, 0.001, INFINITY, rec)) {
      Ray r_out;
      vec3 atten;
      if (scatter(rec.mat_ptr, r_in, rec, atten, r_out) == true) {
        r_in = r_out;
        bcolor *= atten; // normal version
        // bcolor = atten; // crippled version
        depth--;
      } else {
        return vec3(0);
      }
    } else {
      vec3 dir = normalize(r_in.direction);
      float temp = 0.5 * (dir.y + 1.0);
      bcolor *= vec3(1.0 - temp) + temp * vec3(0.5, 0.7, 1.0);
      return bcolor;
    }
  }
}

Scene makeScene() {
  Scene scene;

  Texture ttex1, ttex2;
  ttex1.type = 2;
  ttex1.img = makeImageTexture();

  Material m1;
  m1.type = 0;
  m1.lam = makeLambert(ttex1);

  SceneObj s10, s20;
  s10.type = 0;
  s10.sp = makeSphere(vec3(0, -10, 0), 10, m1);
  scene.ss[0] = s10;
  return scene;
}

void main() {
  // index of global work group
  vec4 pixel = vec4(0.0, 0.0, 0.0, 1.0);
  ivec2 pixel_index = ivec2(gl_GlobalInvocationID.xy);
  ivec2 img_dims = imageSize(img_output); // image dimensions
  int imwidth = img_dims.x;
  int imheight = img_dims.y;
  int i = pixel_index.x;
  int j = pixel_index.y;
  int mdepth = 10;  // 5
  int psample = 30; // 10

  // -------------- declare objects ---------------
  //
  Scene scene = makeScene();

  // camera
  vec3 origin = vec3(13, 2, 3);
  vec3 target = vec3(0, 0, 0);
  vec3 vup = vec3(0, 1, 0);
  float vfov = 20.0;
  float focus_dist = 10.0;
  float aperture = 0.0;
  float t0 = 0.0;
  float t1 = 1.0;
  float aspect_ratio = float(imwidth) / imheight;

  Camera cam = makeCamera(origin, target, vup, vfov, aspect_ratio, aperture,
                          focus_dist, t0, t1);

  vec3 rcolor = vec3(0);

  for (int k = 0; k < psample; k++) {

    float u = float(i + random_double()) / (imwidth - 1);
    float v = float(j + random_double()) / (imheight - 1);
    Ray r = get_ray(cam, u, v);
    rcolor += ray_color(r, scene, mdepth);
  }

  rcolor = fix_color(rcolor, psample);
  // no one ends with 0 in fix color
  //

  // output specific pixel in the image
  imageStore(img_output, pixel_index, vec4(rcolor, 1.0));
}
